%------------------------------------------------------------------
% Лабораторная работа №1.1
% Задание №2-9
% Автор: Бушуев Никта Федорович
% Дата: 16.05.2021
%------------------------------------------------------------------
% 9. [Голицына,Попов,2008,с.220]
% Бутси - коричневая кошка.
% Корни - чёрная кошка.
% Мак - рыжая кошка.
% Флэш, Ровер, Спот - собаки.
% Ровер - рыжая.
% Спот - белая.
% Все животные, которыми владеют Том и Кейт, имеют родословные.
% Том владеет всеми чёрными и коричневыми животными.
% Кейт владеет всеми собаками небелого цвета,  которые не являются
% собственностью Тома.
% Алан владеет Мак, если Кейт не владеет Бутси и если Спот не име-
% ет родословной.
% Флеш - пятнистая собака.
% Определить, какие животные не имеют хозяев.
%------------------------------------------------------------------

predicates
    pet(symbol, symbol, symbol)
    pedigree(symbol)
    owns(symbol, symbol)
    no_owner(symbol)

clauses
    pet("Кошка", "Бутси", "Коричневый"). % Бутси - коричневая кошка.
    pet("Кошка", "Корни", "Черный").     % Корни - чёрная кошка.
    pet("Кошка", "Мак", "Рыжий").        % Мак - рыжая кошка.
    % Флэш, Ровер, Спот - собаки.
    pet("Собака", "Флэш", "Пятнистый").  % Флеш - пятнистая собака.
    pet("Собака", "Ровер", "Рыжий").     % Ровер - рыжая.
    pet("Собака", "Спот", "Белый").      % Спот - белая.

    % Все животные, которыми владеют Том и Кейт, имеют родословные.
    pedigree(P):- owns("Том", P); owns("Кейт", P).
  
    % Том владеет всеми чёрными и коричневыми животными.
    owns("Том", P):- pet(_, P, "Черный"); pet(_, P, "Коричневый").
  
    % Кейт владеет всеми собаками небелого цвета, которые не являются собственностью Тома.
    owns("Кейт", P):- pet("Собака", P, C), C <> "Белый", not(owns("Том", P)).
  
    % Алан владеет Мак, если Кейт не владеет Бутси и если Спот не имеет родословной.
    owns("Алан", "Мак"):- not(owns("Кейт", "Бутси")), not(pedigree("Спот")).
  
    no_owner(P):- pet(_, P, _), not(owns(_, P)).
 
goal
    no_owner(P).